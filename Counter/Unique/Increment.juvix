module Counter.Unique.Increment;

import Stdlib.Prelude open;
import Anoma open;
import Stdlib.Debug.Fail open;
import BaseLayer.ResourceMachine open;
import BaseLayer.TransactionRequest open;
import Counter.Unique.Resource open;
import Anoma.Builtin.System open;
import Applib open;
import Anoma.State.CommitmentTree open using {mkRoot};
import Stdlib.Debug open;

increment
  (randSeed : Nat)
  (currentCounter : Resource)
  (logic : Logic)
  (latestRoot : Nat)
  : Transaction :=
  let
    standardInputs :=
      mkStandardInputs@{
        caller := Universal.identity;
        currentRoot := mkRoot latestRoot;
      };
  in runTx
    randSeed
    standardInputs
    if
      | not (hasCounterKind currentCounter logic) :=
        failwith "The input resource has the wrong kind"
      | else :=
        do {
          nonce <- genRandomNonce;
          let
            newCounter :=
              mkCounter@{
                logic;
                nonce;
                uniqueLabel := currentCounter |> Resource.label |> builtinAnomaDecode;
                count := Resource.value currentCounter + 1;
                ephemeral := false;
              };
          in
          prepareStandardTransaction@{
            consumed := [currentCounter];
            created := [newCounter];
          };
        };

main
  (randSeed : Nat)
  (currentCounter : Encoded Resource)
  (logic : Encoded Logic)
  (latestRoot : Nat)
  : TransactionRequest :=
  increment
      randSeed
      (decode currentCounter)
      (decode logic)
      latestRoot
    |> TransactionRequest.fromTransaction;
