module WebHelloWorld;

import Stdlib.Prelude open;
import Applib open;
import AppIdentity open using {label};

--- Creates a new ;Resource; that stores a ;String; message.
--- @param nonce A number used to ensure resource uniqueness
--- @param message The message to store in the ;Resource;.
mkWebHelloWorldResource
  (logic : Logic)
  (nonce : Nonce)
  (message : String)
  {ephemeral : Bool := false}
  : Resource :=
  Resource.mk@{
    label;
    logic := Encoded.encode logic;
    value := AnomaAtom.fromNat (builtinAnomaEncode message);
    quantity := 1;
    nonce := Nonce.toRaw nonce;
    ephemeral;
    unusedRandSeed := 0;
    nullifierKeyCommitment := 0;
  };

--- Produces a ;Transaction; that creates a WebHelloWorld ;Resource;
--- @param nonce A number used to ensure ;Resource; uniqueness.
--- @param message The message to store in the ;Resource;
webHelloWorldTransaction
  {M : Type -> Type}
  {{Monad M}}
  {{Tx M}}
  (logic : Logic)
  (nonce : Nonce)
  (message : String)
  : M Transaction :=
  do {
    let
      newResource := mkWebHelloWorldResource logic nonce message;
    in
    mkTransactionNoPublicData@{
      -- A Transaction must be balanced, so we consume an ephemeral resource of
      -- the same kind as the one we're creating.
      consumed := [newResource@Resource{ephemeral := true}];
      created := [newResource];
    };
  };

ctx : TxContext :=
  TxContext.mk@{
    caller := Universal.identity;
    currentRoot := CommitmentRoot.fromNat 0;
  };

--- The function that is run to produce a Transaction to send to Anoma.
main (logic : Logic) (message : String) : TransactionRequest :=
  TransactionRequest.build 0 ctx (webHelloWorldTransaction logic (Nonce.fromAnomaAtom 0) message);
